buildscript {
    ext {
        kotlin_version = '1.4.21'
        serialization_version = '1.0.0'
        jvm_target = '1.8'
        junit_version = '5.2.0'
        log4j2_version = '2.11.1'
        asm_version = '8.0.1'
        protobuf_plugin_version = '0.8.13'
        protobuf_version = '3.13.0'
        grpc_version = '1.33.1'
        grpc_kotlin_version = '0.2.1'
    }

    repositories {
        jcenter()
    }
    dependencies {
        classpath group: 'org.jetbrains.kotlin', name: 'kotlin-gradle-plugin', version: kotlin_version
        classpath group: 'org.jetbrains.kotlin', name: 'kotlin-serialization', version: kotlin_version
        classpath group: 'com.google.protobuf', name: 'protobuf-gradle-plugin', version: protobuf_plugin_version
    }
}

apply plugin: 'kotlin'
apply plugin: 'kotlinx-serialization'

allprojects {
    apply plugin: 'kotlin'
    apply plugin: 'kotlinx-serialization'

    group 'net.cydhra'
    version '0.1.0'

    repositories {
        jcenter()
        maven { url 'https://dl.bintray.com/kotlin/exposed/' }
        maven { url 'https://jitpack.io/' }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            kotlinOptions.jvmTarget = jvm_target
        }
    }
}

project("acromantula_backend") {
    apply plugin: 'com.google.protobuf'

    protobuf {
        protoc {
            artifact = "com.google.protobuf:protoc:$protobuf_version"
        }

        plugins {
            client {
                artifact = 'com.github.googleapis:gapic-generator-kotlin:master-SNAPSHOT:core@jar'
            }
            grpc {
                artifact = "io.grpc:protoc-gen-grpc-java:1.20.0"
            }
        }
        generateProtoTasks {
            ofSourceSet("main").forEach {
                it.plugins {
                    // generate client side stuff and message builders
                    client {
                        option("test-output=build/generated/source/clientTest")
                    }

                    // generate server API
                    grpc {}
                }
            }
        }
    }

    sourceSets {
        bus {
            dependencies {
                // kotlin
                implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8'
                implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-core', version: '1.3.5'
                implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-serialization-protobuf',
                        version: serialization_version

                // utility
                implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.9'
                implementation group: 'com.google.guava', name: 'guava', version: '28.1-jre'
                implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.6'

                // logging
                implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: log4j2_version
                implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: log4j2_version
                implementation group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: log4j2_version
            }
        }

        workspace {
            compileClasspath += sourceSets.bus.runtimeClasspath

            dependencies {
                // bytecode
                implementation group: 'org.ow2.asm', name: 'asm', version: asm_version
                implementation group: 'org.ow2.asm', name: 'asm-tree', version: asm_version

                // database
                implementation group: 'org.jetbrains.exposed', name: 'exposed', version: '0.17.6'
                implementation group: 'com.h2database', name: 'h2', version: '1.4.197'
            }
        }

        features {
            compileClasspath += sourceSets.bus.runtimeClasspath
            compileClasspath += sourceSets.workspace.runtimeClasspath
        }

        main {
            compileClasspath += sourceSets.bus.runtimeClasspath
            compileClasspath += sourceSets.workspace.runtimeClasspath
            compileClasspath += sourceSets.features.runtimeClasspath
            runtimeClasspath += sourceSets.bus.runtimeClasspath
            runtimeClasspath += sourceSets.workspace.runtimeClasspath
            runtimeClasspath += sourceSets.features.runtimeClasspath

            java.srcDirs += "${protobuf.generatedFilesBaseDir}/main/java"
            java.srcDirs += "${protobuf.generatedFilesBaseDir}/main/grpc"
            java.srcDirs += "${protobuf.generatedFilesBaseDir}/main/client"
        }

        test {
            compileClasspath += sourceSets.bus.runtimeClasspath
            compileClasspath += sourceSets.workspace.runtimeClasspath
            compileClasspath += sourceSets.features.runtimeClasspath
            runtimeClasspath += sourceSets.bus.runtimeClasspath
            runtimeClasspath += sourceSets.workspace.runtimeClasspath
            runtimeClasspath += sourceSets.features.runtimeClasspath

            java.srcDirs += "${project.buildDir}/generated/source/clientTest"
        }
    }

    // main source set and test
    dependencies {
        // kotlin
        implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8'
        implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-core', version: '1.3.5'
        implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-serialization-protobuf',
                version: serialization_version

        // protobuf
        implementation group: 'com.google.protobuf', name: 'protobuf-java', version: protobuf_version

        // grpc
        implementation group: 'com.github.googleapis.gax-kotlin', name: 'kgax-grpc', version: 'master-SNAPSHOT'
        implementation group: 'io.grpc', name: 'grpc-netty-shaded', version: grpc_version
        implementation group: 'io.grpc', name: 'grpc-services', version: grpc_version

        // utility
        implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.9'
        implementation group: 'com.google.guava', name: 'guava', version: '28.1-jre'
        implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.6'

        // logging
        implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: log4j2_version
        implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: log4j2_version
        implementation group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: log4j2_version

        // argument parsing for CLI
        implementation group: 'com.xenomachina', name: 'kotlin-argparser', version: '2.0.7'

        // bytecode
        implementation group: 'org.ow2.asm', name: 'asm', version: asm_version
        implementation group: 'org.ow2.asm', name: 'asm-tree', version: asm_version

        // database
        implementation group: 'org.jetbrains.exposed', name: 'exposed', version: '0.17.6'
        implementation group: 'com.h2database', name: 'h2', version: '1.4.197'

        // testing (mocking required for autogenerated grpc tests)
        testImplementation group: 'org.jetbrains.kotlin', name: 'kotlin-test'
        testImplementation group: 'org.jetbrains.kotlin', name: 'kotlin-test-junit'
        testImplementation group: 'com.nhaarman', name: 'mockito-kotlin', version: '1.6.0'
        testImplementation group: 'org.mockito', name: 'mockito-core', version: '2.23.4'
    }

    test {
        useJUnit()
        testLogging {
            events "passed", "skipped", "failed"
        }
    }

    jar {
        from(sourceSets.bus.output)
        from(sourceSets.workspace.output)
        from(sourceSets.features.output)

        manifest {
            attributes "Implementation-Title": project.name,
                    "Implementation-Version": project.version,
                    "Main-Class": "net.cydhra.acromantula.AcromantulaServiceKt"
        }
    }
}
